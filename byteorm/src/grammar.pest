WHITESPACE = _{ " " | "\t" }
COMMENT    = _{ "#" ~ (!NEWLINE ~ ANY)* }

ident        = @{ (ASCII_ALPHANUMERIC | "_")+ }
type_name    = @{ (ASCII_ALPHANUMERIC | "_")+ ~ ("?")? }

attr         = { "@" ~ attr_name ~ args? }
attr_name    = @{ (ASCII_ALPHANUMERIC | "_")+ }

modifier     = ${ ident ~ args? }

args         = { "(" ~ arg_content ~ ")" }
arg_content  = @{ arg_char* }
arg_char     = {
    "(" ~ arg_char* ~ ")" ~ arg_char*
    | !(")") ~ ANY       
}

nl           = _{ NEWLINE+ }

schema       = { SOI ~ nl? ~ (enum_decl | model_decl)* ~ nl? ~ EOI }
field_decl   = { ident ~ type_name ~ field_mods* ~ nl }
field_mods   = _{ modifier | attr }
string       = @{ "\"" ~ ( "\\\"" | (!"\"" ~ ANY) )* ~ "\"" }

computed_attr = { "@" ~ "computed" ~ WHITESPACE? ~ ident ~ WHITESPACE? ~ "=" ~ WHITESPACE? ~ string }

computed_decl = { computed_attr ~ nl }

model_attr = { "@@" ~ attr_name ~ args? ~ nl }

model_member = { field_decl | computed_decl | model_attr }

model_decl   = { "model" ~ ident ~ "{" ~ nl? ~ model_member+ ~ nl? ~ "}" ~ nl? }

enum_value   = { ident ~ nl }
enum_decl    = { "enum" ~ ident ~ "{" ~ nl? ~ enum_value+ ~ nl? ~ "}" ~ nl? }
